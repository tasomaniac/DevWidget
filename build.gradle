buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')

    def versionMajor = 1
    def versionMinor = 2
    def versionBuild = 1

    ext.versions = [
            compileSdk : 28,
            buildTools : "28.0.1",
            minSdk     : 21,
            targetSdk  : 25,
            versionCode: versionMajor * 10000 + versionMinor * 1000 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            arch       : '1.1.1',
            dagger     : '2.16',
            kotlin     : '1.2.51',
            room       : '1.1.1',
            supportLib : '28.0.0-alpha3',
    ]

    repositories {
        gradlePluginPortal()
        google()
        jcenter()
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }
    
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-beta04'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
        classpath 'com.novoda:gradle-android-command-plugin:2.0.1'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.5.2'
        if (isPlay) {
            classpath 'com.google.gms:google-services:4.0.2'
            classpath 'io.fabric.tools:gradle:1.25.4'
        }
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        google()
        jcenter()
    }
}
